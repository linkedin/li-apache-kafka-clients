/*
 * Copyright 2017 LinkedIn Corp.
 * Licensed under the BSD 2-Clause License (the "License").â€¨
 * See License in the project root for license information.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
    }
}

group = 'com.linkedin.kafka.clients'

import com.linkedin.gradle.build.DistributeTask

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

//project dependencies

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.kafka:kafka-clients:0.10.2.1'

    testCompile 'org.bouncycastle:bcpkix-jdk15on:1.54'
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'com.101tec:zkclient:0.7'
    testCompile 'org.testng:testng:6.11'

    testRuntime 'org.apache.kafka:kafka_2.10:0.10.2.1'
}

//test configurations

test {
    useTestNG {}
    maxHeapSize = "1024m"
    testLogging {
        events "passed", "failed", "skipped"
    }
}

test.dependsOn('checkstyleMain', 'checkstyleTest')

//code quality and inspections

checkstyle {
    toolVersion = '8.0'
}

//build artifacts

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from sourceSets.test.output
}

build.dependsOn 'testJar'

configurations {
    testArtifacts.extendsFrom testRuntime
}

artifacts {
    testArtifacts testJar
    archives sourcesJar
    archives javadocJar
}

//maven artifact(s) generation

publishing {
    publications {
        standardjava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact testJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    description 'li-apache-kafka-clients'
                    url 'https://github.com/linkedin/li-apache-kafka-clients'
                    scm {
                        url 'https://github.com/linkedin/li-apache-kafka-clients'
                        connection 'scm:git:git://github.com/linkedin/li-apache-kafka-clients.git'
                        developerConnection 'scm:git:ssh:git@github.com:linkedin/li-apache-kafka-clients.git.git'
                    }
                    licenses {
                        license {
                            name 'BSD 2-CLAUSE LICENSE'
                            url 'https://opensource.org/licenses/BSD-2-Clause'
                            distribution 'repo'
                        }
                    }
                }
            }
            pom.withXml {
                asNode().children().get(4).children().remove(0) //clear dependencies
                asNode().children().get(4).appendNode("dependency")
                .appendNode("groupId", "org.apache.kafka").parent()
                .appendNode("artifactId", "kafka-clients").parent()
                .appendNode("version", "0.10.2.1").parent()
                .appendNode("scope", "compile").parent()
                .parent()
                .appendNode("dependency")
                .appendNode("groupId", "commons-io").parent()
                .appendNode("artifactId", "commons-io").parent()
                .appendNode("version", "2.5").parent()
                .appendNode("scope", "test").parent()
            }
        }
    }
}

//publishing (artifactory)

artifactory {
    publish {
        contextUrl = 'https://linkedin.jfrog.io/linkedin'
        repository {
            repoKey = 'li-apache-kafka-clients'
            username = System.getenv('ARTIFACTORY_USER') ?: ""
            password = System.getenv('ARTIFACTORY_KEY') ?: ""
        }
        defaults {
            publications ('standardjava')
            publishBuildInfo = true
            publishArtifacts = true
            publishPom = true
            publishIvy = true
        }
    }
}

/**
 * Sends stuff to artifactory and then onto bintry/jcenter
 */
task distributeBuild(type: DistributeTask) {
    dependsOn ':artifactoryPublish'
}

//wrapper generation task

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}